module Brat.Compiler (printAST
                     ,printDeclsHoles
                     ,writeDot
                     ,compileFile
                     ,compileAndPrintFile
                     ,compileToGraph
                     ,CompilingHoles(..)
                     ) where

import Brat.Checker.Types (TypedHole, Modey(Kerny), VEnv)
import Brat.Compile.Hugr
import Brat.Dot (toDotString)
import Brat.Elaborator
import Brat.Error
import Brat.Graph(Graph, Node(BratNode), NodeType(Box, Id))
import Brat.Load
import Brat.Naming (Namespace, root, split, Name)
import Brat.QualName (QualName)
import Brat.Syntax.Port (NamedPort(..), OutPort(..), InPort(..))
import Brat.Syntax.Value (Val(VFun))

import Control.Exception (evaluate)
import Control.Monad (forM, when)
import Control.Monad.Except
import Data.List (intercalate)
import qualified Data.Map as M
import qualified Data.ByteString.Lazy as BS
import Data.Foldable (for_)
import Data.HugrGraph (HugrGraph, NodeId, to_json)
import System.Exit (die)

printDeclsHoles :: [FilePath] -> String -> IO ()
printDeclsHoles libDirs file = do
  env <- runExceptT $ loadFilename root libDirs file
  (declEnv, holes, _, _, _) <- eitherIO env
  putStrLn "Decls:"
  forM (M.toList declEnv) $ \(name, (src_tys, _vdecl)) ->
    putStrLn $ show name ++ " :: " ++ intercalate ", " (map (show . snd) src_tys)
  putStrLn ""
  putStrLn "Holes:"
  mapM_ print holes

-- Print an 80 column banner as the header and footer of some IO action's output
banner :: String -> IO a -> IO a
banner s m = putStrLn startText *> m <* putStrLn endText
 where
  startText = dashes ++ " " ++ s ++ space ++ dashes
  endText = replicate 80 '-'

  -- Add an extra space if `s` is odd to pad to 80 chars
  space = ' ' : replicate (len `mod` 2) ' '
  dashes = replicate (39 - hlen) '-'
  len = length s + 2
  hlen = len `div` 2

printAST :: Bool -> Bool -> String -> IO ()
printAST printRaw printAST file = do
  cts <- readFile file
  (_, env@(decls,_)) <- eitherIO $ parseFile file cts
  banner "Flat AST" $ mapM_ print decls
  env'@(decls, _, _) <- eitherIO $ addSrcContext file cts (elabEnv env)
  when printRaw $ banner "Raw AST" $ mapM_ print decls
  when printAST $
    banner "desugared AST" (mapM_ print =<< eitherIO (addSrcContext file cts (desugarEnv env')))

writeDot :: [FilePath] -> String -> String -> IO ()
writeDot libDirs file out = do
  env <- runExceptT $ loadFilename root libDirs file
  (_, _, _, graph, cs) <- eitherIO env
  writeFile out (toDotString graph cs)
{-
 where
  isMain (PrefixName [] "main", _) = True
  isMain _ = False
-}

newtype CompilingHoles = CompilingHoles [TypedHole]

instance Show CompilingHoles where
  show (CompilingHoles hs) = unlines $
    "Can't compile file with remaining holes": fmap (("  " ++) . show) hs

compileToGraph :: [FilePath] -> String -> IO (Namespace, VMod)
compileToGraph libDirs file = do
  let (checkRoot, newRoot) = split "checking" root
  env <- runExceptT $ loadFilename checkRoot libDirs file
  (newRoot,) <$> eitherIO env

-- Map from box name to (compiled hugr, list of hole nodes in it)
type CompilationResult = M.Map Name (HugrGraph NodeId, [NodeId])

compileFile :: [FilePath] -> String -> IO (Either CompilingHoles CompilationResult)
compileFile libDirs file = do
  (newRoot, (declEnv, holes, st, outerGraph, _)) <- compileToGraph libDirs file
  let venv = M.map fst declEnv
  case holes of
    [] -> let box_decls = (M.keys declEnv) >>= (findBoxes venv outerGraph)
          in Right <$> (evaluate -- turns 'error' into IO 'die'
            $ M.fromList [(n, let (hugr, holes) = compileKernel (newRoot, st, outerGraph) "root" n
                               in (hugr, map fst holes))
                         | n <- box_decls])
    hs -> pure $ Left (CompilingHoles hs)
 where
  findBoxes :: VEnv -> Graph -> QualName -> [Name]
  findBoxes venv (ns, es) name = case M.lookup name venv of
        Nothing -> error $ (show name) ++ ".... not found in VEnv"
        Just vals -> vals >>= \(NamedPort (Ex n _) _, _) -> case M.lookup n ns of
            Just (BratNode Id _ _) ->
               [src | (Ex src 0, _, In tgt _) <- es, tgt == n, isKernelBox src ns]
            _ -> []
  isKernelBox :: Name -> M.Map Name Node -> Bool
  isKernelBox name ns
    | Just (BratNode (Box _ _ ) [] [(_, VFun Kerny _cty)]) <- M.lookup name ns = True
    | otherwise = False

compileAndPrintFile :: [FilePath] -> String -> IO ()
compileAndPrintFile libDirs file = compileFile libDirs file >>= \case
  Right hs -> for_ (M.toList hs) $ \(n, (hugr, splices)) -> do
    putStrLn $ "Compiled box: " ++ show n
    BS.putStr (to_json hugr)
    putStrLn $ "With splices: " ++ show splices
  Left err -> die (show err)
